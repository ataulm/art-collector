apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    buildFeatures {
        // enables compose for this module (TODO: is this the right module to put it in?)
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        // kotlin intermediate representation
        useIR = true
    }

    composeOptions {
        kotlinCompilerVersion versions.kotlin
        kotlinCompilerExtensionVersion versions.kotlinCompose
    }

    compileSdkVersion versions.androidSdk.compile

    defaultConfig {
        applicationId 'com.ataulm.artcollector'
        minSdkVersion versions.androidSdk.min
        targetSdkVersion versions.androidSdk.target
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'String', 'HARVARD_KEY', "\"${harvard_art_museums_api_key}\""
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-rules-moshi.pro',
                    'proguard-rules-moshi-kotlin.pro',
                    'proguard-rules-retrofit.pro'
        }
    }

    dynamicFeatures = [':artist', ':painting']
}

dependencies {
    api project(':domain')
    api libraries.androidxAppCompat
    api libraries.androidxConstraintLayout
    api libraries.androidxLegacySupportV13
    api libraries.androidxLifecycleExtensions
    api libraries.androidxMaterial
    api libraries.androidxRecyclerView
    api libraries.dagger
    api libraries.glide
    // TODO: what's in here? is this the "base" one or is "compose ui"?
//    api libraries.kotlinComposeFoundation
    api libraries.kotlinComposeUi
    api libraries.kotlinComposeUiTooling
    api libraries.kotlinCoroutinesAndroid
    api libraries.kotlinCoroutinesCore
    api libraries.kotlinStdLib
    api libraries.photoView

    implementation libraries.androidxPagingRuntime
    debugImplementation libraries.chuck
    releaseImplementation libraries.chuckNoOp
    implementation libraries.moshi
    implementation libraries.retrofit
    implementation libraries.retrofitMoshi

    kapt libraries.daggerAndroid
    kapt libraries.daggerCompiler
    kapt libraries.moshiKotlinCodegen

    androidTestImplementation libraries.androidxTestRules
    androidTestImplementation libraries.androidxTestRunner
    androidTestImplementation libraries.androidxEspressoCore

    testImplementation libraries.googleTruth
    testImplementation libraries.jUnit
    testImplementation libraries.mockitoCore
    testImplementation libraries.mockitoKotlin
}
